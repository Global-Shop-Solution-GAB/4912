Program.Sub.ScreenSU.Start
Gui.frmUserFields..create
Gui.frmUserFields..caption("Inventory Extra User Fields")
Gui.frmUserFields..size(8400,10515)
Gui.frmUserFields..position(0,0)
Gui.frmUserFields..event(UnLoad,frmTitles_UnLoad)
Gui.frmUserFields..forecolor(0)
Gui.frmUserFields..fontstyle(False,False,False,False,False)
Gui.frmUserFields..BackColor(-2147483633)
Gui.frmUserFields..minbutton(False)
Gui.frmUserFields..mousepointer(0)
Gui.frmUserFields..sizeable(False)
Gui.frmUserFields..MinX(0)
Gui.frmUserFields..MinY(0)
Gui.frmUserFields.GsGCUserFields.Create(GsGridControl)
Gui.frmUserFields.GsGCUserFields.Size(8235,9810)
Gui.frmUserFields.GsGCUserFields.Position(30,0)
Gui.frmUserFields.GsGCUserFields.Anchor(15)
Gui.frmUserFields.GsGCUserFields.Event(RowCellClick,GsGCTitles_RowCellClick)
Gui.frmUserFields.cmdSave.Create(Button)
Gui.frmUserFields.cmdSave.Size(855,345)
Gui.frmUserFields.cmdSave.Position(7410,9840)
Gui.frmUserFields.cmdSave.Anchor(10)
Gui.frmUserFields.cmdSave.Caption("Save")
Gui.frmUserFields.cmdSave.Event(Click,cmdSave_Click)
Gui.frmValues..Create
Gui.frmValues..Size(6150,5430)
Gui.frmValues..MinX(0)
Gui.frmValues..MinY(0)
Gui.frmValues..Position(0,0)
Gui.frmValues..BackColor(-2147483633)
Gui.frmValues..MousePointer(0)
Gui.frmValues..Event(UnLoad,frmValues_UnLoad)
Gui.frmValues..Caption("Select a Value:")
Gui.frmValues.GsGCValues.Create(GsGridControl)
Gui.frmValues.GsGCValues.Size(5970,5115)
Gui.frmValues.GsGCValues.Position(30,15)
Gui.frmValues.GsGCValues.Event(RowCellClick,GsGCValues_RowCellClick)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
	V.Global.sPart.Declare
	V.Global.sLoc.Declare
	V.Global.iRowIndex.Declare
	V.Global.bEdit.Declare
Program.Sub.Preflight.End

Program.Sub.Main.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sIcon.Declare

	F.Intrinsic.String.Build("{0}\gss2.ico",V.Caller.GasDir,V.Local.sIcon)
	Gui.frmUserFields..Icon(V.Local.sIcon)
	Gui.frmValues..Icon(V.Local.sIcon)
	Gui.frmUserFields.cmdSave.Enabled(False)
	
	F.Intrinsic.Control.SelectCase(V.Caller.Hook)
		'Inventory Master (Script 2 Button)
		F.Intrinsic.Control.Case(10120)
			V.Passed.000070.Set("Inv Extra UF")
		
		F.Intrinsic.Control.Case(10230)
			F.Intrinsic.Control.If(V.Passed.000007,=,"")
				F.Intrinsic.UI.Msgbox("Please Select a Part Number.","Inv Extra User Fields")
				F.Intrinsic.Control.End
			F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.If(V.Caller.Switches,=,"O")
				V.Global.bEdit.Set(True)
				Gui.frmUserFields.cmdSave.Enabled(True)
			F.Intrinsic.Control.EndIf
			F.Intrinsic.String.GSSPartString(V.Passed.000007,V.Passed.000008,V.Global.sPart)
			V.Global.sLoc.Set(V.Passed.000009)
			F.Intrinsic.Control.CallSub(SetContextMenus)
			F.Intrinsic.Control.CallSub(CheckTable)
			F.Intrinsic.Control.CallSub(LoadUserFields)
			Gui.frmUserFields..Show
		
		'Supply And Demand (Script 2 Button)
		F.Intrinsic.Control.Case(14045)
			V.Passed.000190.Set("Inv Extra UF")
		
		F.Intrinsic.Control.Case(14080)
			F.Intrinsic.Control.If(V.Passed.000003,=,"")
				F.Intrinsic.UI.Msgbox("Please Select a Part Number.","Inv Extra User Fields")
				F.Intrinsic.Control.End
			F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.If(V.Caller.Switches,=,"M")
				V.Global.bEdit.Set(True)
				Gui.frmUserFields.cmdSave.Enabled(True)
			F.Intrinsic.Control.EndIf
			F.Intrinsic.String.GSSPartString(V.Passed.000003,V.Passed.000099,V.Global.sPart)
			V.Global.sLoc.Set(V.Passed.000004)
			F.Intrinsic.Control.CallSub(SetContextMenus)
			F.Intrinsic.Control.CallSub(CheckTable)
			F.Intrinsic.Control.CallSub(LoadUserFields)
			Gui.frmUserFields..Show

	F.Intrinsic.Control.EndSelect
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.frmTitles_UnLoad.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	F.Intrinsic.Control.End

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.frmTitles_UnLoad.End

Program.Sub.CheckTable.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	V.Local.bExists.Declare
	
	F.ODBC.Connection!con.OpenCompanyConnection(300)
	
	F.ODBC.Connection!con.TableExists("GAB_4912_TITLES",V.Local.bExists)
	F.Intrinsic.Control.If(V.Local.bExists,=,False)
		F.ODBC.Connection!con.Execute("CREATE TABLE GAB_4912_TITLES(TID IDENTITY,USER1 CHAR(30),USER2 CHAR(30),USER3 CHAR(30),USER4 CHAR(30),USER5 CHAR(30),USER6 CHAR(30),USER7 CHAR(30),USER8 CHAR(30),USER9 CHAR(30),USER10 CHAR(30),USER11 CHAR(30),USER12 CHAR(30),USER13 CHAR(30),USER14 CHAR(30),USER15 CHAR(30),USER16 CHAR(30),USER17 CHAR(30),USER18 CHAR(30),USER19 CHAR(30),USER20 CHAR(30),USER21 CHAR(30),USER22 CHAR(30),USER23 CHAR(30),USER24 CHAR(30),USER25 CHAR(30),USER26 CHAR(30),USER27 CHAR(30),USER28 CHAR(30),USER29 CHAR(30),USER30 CHAR(30),UNIQUE(TID));")
	F.Intrinsic.Control.EndIf
	
	F.ODBC.Connection!con.TableExists("GAB_4912_VALUES",V.Local.bExists)
	F.Intrinsic.Control.If(V.Local.bExists,=,False)
		F.ODBC.Connection!con.Execute("CREATE TABLE GAB_4912_VALUES(VID IDENTITY,USER_FIELD CHAR(6),VALUE CHAR(50),UNIQUE(VID));")
	F.Intrinsic.Control.EndIf
	
	F.ODBC.Connection!con.TableExists("GAB_4912_USER_FIELDS",V.Local.bExists)
	F.Intrinsic.Control.If(V.Local.bExists,=,False)
		F.ODBC.Connection!con.Execute("CREATE TABLE GAB_4912_USER_FIELDS(UFID IDENTITY,PART CHAR(20),LOCATION CHAR(2),USER1 CHAR(50),USER2 CHAR(50),USER3 CHAR(50),USER4 CHAR(50),USER5 CHAR(50),USER6 CHAR(50),USER7 CHAR(50),USER8 CHAR(50),USER9 CHAR(50),USER10 CHAR(50),USER11 CHAR(50),USER12 CHAR(50),USER13 CHAR(50),USER14 CHAR(50),USER15 CHAR(50),USER16 CHAR(50),USER17 CHAR(50),USER18 CHAR(50),USER19 CHAR(50),USER20 CHAR(50),USER21 CHAR(50),USER22 CHAR(50),USER23 CHAR(50),USER24 CHAR(50),USER25 CHAR(50),USER26 CHAR(50),USER27 CHAR(50),USER28 CHAR(50),USER29 CHAR(50),USER30 CHAR(50),UNIQUE(UFID));")
		F.ODBC.Connection!con.Execute("CREATE INDEX GAB_4912_USR_FLDSK01 USING 1 ON GAB_4912_USER_FIELDS(PART);")
		F.ODBC.Connection!con.Execute("CREATE INDEX GAB_4912_USR_FLDSK02 USING 2 ON GAB_4912_USER_FIELDS(LOCATION);")
	F.Intrinsic.Control.EndIf
	
	F.ODBC.Connection!con.Close

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.CheckTable.End

Program.Sub.SetContextMenus.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	Gui.frmUserFields..ContextMenuCreate("ctxTitles")
	Gui.frmUserFields.GsGCUserFields.ContextMenuAttach("ctxTitles")
	Gui.frmUserFields..ContextMenuAddItem("ctxTitles","Refresh",0,"Refresh")
	Gui.frmUserFields..ContextMenuAddItem("ctxTitles","Export",0,"Export")
	Gui.frmUserFields..ContextMenuSetItemEventHandler("ctxTitles","Refresh","LoadUserFields")
	Gui.frmUserFields..ContextMenuSetItemEventHandler("ctxTitles","Export","MenuClickExport")
	
	Gui.frmValues..ContextMenuCreate("ctxValues")
	Gui.frmValues.GsGCValues.ContextMenuAttach("ctxValues")
	Gui.frmValues..ContextMenuAddItem("ctxValues","Export",0,"Export")
	Gui.frmValues..ContextMenuSetItemEventHandler("ctxValues","Export","MenuClickExport")
		
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.SetContextMenus.End

Program.Sub.MenuClickExport.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sFileExport.Declare
	V.Local.bExcel.Declare
	V.Local.bFileLocked.Declare
	V.Local.sMsg.Declare
	V.Local.sForm.Declare
	V.Local.sControl.Declare
	
	F.Intrinsic.Control.SelectCase(V.Args.MenuName)
		F.Intrinsic.Control.Case("CTXTITLES")
			V.Local.sForm.Set("frmUserFields")
			V.Local.sControl.Set("GsGCUserFields")
		F.Intrinsic.Control.Case("CTXVALUES")
			V.Local.sForm.Set("frmValues")
			V.Local.sControl.Set("GsGCValues")
	F.Intrinsic.Control.EndSelect

	F.Automation.MSExcel.CheckPresence(V.Local.bExcel)
	F.Intrinsic.Control.If(V.Local.bExcel)
		F.Intrinsic.String.Build("{0}\GAB_4912_UF_{1}_Export.xlsx",V.Caller.LocalGssTempDir,V.Local.sControl,V.Local.sFileExport)
		F.Intrinsic.File.IsFileLocked(V.Local.sFileExport,V.Local.bFileLocked)
		F.Intrinsic.Control.If(V.Local.bFileLocked)
			F.Intrinsic.String.Build("File is already open. Please close and export again.{0}File: {1}",V.Ambient.NewLine,V.Local.sFileExport,V.Local.sMsg)
			F.Intrinsic.UI.Msgbox(V.Local.sMsg,"File In Use")
		F.Intrinsic.Control.Else
			Gui.[V.Local.sForm].[V.Local.sControl].Export(V.Local.sFileExport,"xlsx")
			F.Intrinsic.Task.ShellExec(0,"",V.Local.sFileExport,"","",1)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Build("{0}\GAB_4912_UF_{1}_Export.csv",V.Caller.LocalGssTempDir,V.Local.sControl,V.Local.sFileExport)
		F.Intrinsic.File.IsFileLocked(V.Local.sFileExport,V.Local.bFileLocked)
		F.Intrinsic.Control.If(V.Local.bFileLocked)
			F.Intrinsic.String.Build("File is already open. Please close and export again.{0}File: {1}",V.Ambient.NewLine,V.Local.sFileExport,V.Local.sMsg)
			F.Intrinsic.UI.Msgbox(V.Local.sMsg,"File In Use")
		F.Intrinsic.Control.Else
			Gui.[V.Local.sForm].[V.Local.sControl].Export(V.Local.sFileExport,"csv")
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Task.ShellExec(0,"",V.Local.sFileExport,"","",1)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.MenuClickExport.End

Program.Sub.LoadUserFields.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	V.Local.iCnt.Declare
	V.Local.iRow.Declare
	V.Local.sUserField.Declare
	V.Local.sTitle.Declare
	V.Local.sSQL.Declare
	
	F.Intrinsic.Control.If(V.DataTable.dtUserFields.Exists)
		F.Data.DataTable.Close("dtUserFields")
		F.Data.DataTable.Close("dtUFVals")
	F.Intrinsic.Control.EndIf
	
	F.ODBC.Connection!con.OpenCompanyConnection(300)
	
	F.Data.DataTable.Create("dtUserFields",True)
	F.Data.DataTable.AddColumn("dtUserFields","UserField",String,"")
	F.Data.DataTable.AddColumn("dtUserFields","Title",String,"")
	F.Data.DataTable.AddColumn("dtUserFields","Value",String,"",50)
	F.Data.DataTable.AddColumn("dtUserFields","Value_BRW",String,"Browse")
	
	'Load Titles for User Fields
	F.Data.DataTable.CreateFromSQL("dtTitles","con","Select * From GAB_4912_TITLES Where TID = 1;",True)
	F.Intrinsic.Control.For(V.Local.iCnt,1,30,1)
		F.Intrinsic.Math.Sub(V.Local.iCnt,1,V.Local.iRow)
		F.Intrinsic.String.Build("USER{0}",V.Local.iCnt,V.Local.sUserField)
		F.Data.DataTable.AddRow("dtUserFields","UserField",V.Local.sUserField)
		F.Intrinsic.Control.If(V.DataTable.dtTitles(0).[V.Local.sUserField]!FieldValTrim,=,"")
			F.Data.DataTable.SetValue("dtUserFields",V.Local.iRow,"Title",V.Local.sUserField)
		F.Intrinsic.Control.Else
			F.Data.DataTable.SetValue("dtUserFields",V.Local.iRow,"Title",V.DataTable.dtTitles(0).[V.Local.sUserField]!FieldValTrim)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Data.DataTable.Close("dtTitles")
	
	'Load Values
	F.Intrinsic.String.Build("Select * From GAB_4912_USER_FIELDS Where Part = '{0}' And Location = '{1}';",V.Global.sPart,V.Global.sLoc,V.Local.sSQL)
	F.Data.DataTable.CreateFromSQL("dtUFVals","con",V.Local.sSQL,True)
	F.Intrinsic.Control.If(V.DataTable.dtUFVals.RowCount,<>,0)
		F.Intrinsic.Control.For(V.Local.iCnt,1,30,1)
			F.Intrinsic.Math.Sub(V.Local.iCnt,1,V.Local.iRow)
			F.Intrinsic.String.Build("USER{0}",V.Local.iCnt,V.Local.sUserField)
			F.Data.DataTable.SetValue("dtUserFields",V.Local.iRow,"Value",V.DataTable.dtUFVals(0).[V.Local.sUserField]!FieldValTrim)
		F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Intrinsic.Control.EndIf
	
	Gui.frmUserFields.GsGCUserFields.AddGridviewFromDatatable("gvUserFields","dtUserFields")
	Gui.frmUserFields.GsGCUserFields.SuspendLayout()
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","MultiSelect",True)
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","AllowSort",False)
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","AllowFilter",True)
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","OptionsDetailShowDetailTabs",False)
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","OptionsViewShowGroupPanel",False)
	Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","Enableappearanceoddrow",True)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","Visible",False)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","ShowCaption",False)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","ShowCaption",False)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","ShowCaption",False)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","Icon","Browse")
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","Caption","User Field")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","Caption","Title")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","Caption","Value")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","Caption","Browser")
	
	F.Intrinsic.Control.If(V.Global.bEdit)
		Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","AllowEdit",False)
		Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","AllowEdit",False)
		Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","AllowEdit",True)
		Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","AllowEdit",False)
		
		Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","ReadOnly",False)
	F.Intrinsic.Control.Else
		Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","Editable",False)
		Gui.frmUserFields.GsGCUserFields.SetGridViewProperty("gvUserFields","ReadOnly",True)
	F.Intrinsic.Control.EndIf
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","MinWidth","200")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","MinWidth","200")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","MinWidth","300")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","MinWidth","15")
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","MaxLength",50)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","HeaderFontBold",True)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","HeaderFontBold",True)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","HeaderFontBold",True)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","HeaderFontBold",True)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","CellFontBold",True)
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","CellFontBold",True)
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","HeaderHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","HeaderHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","HeaderHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","HeaderHAlignment","Center")
	
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","UserField","CellHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Title","CellHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value","CellHAlignment","Center")
	Gui.frmUserFields.GsGCUserFields.SetColumnProperty("gvUserFields","Value_BRW","CellHAlignment","Center")
	
	Gui.frmUserFields.GsGCUserFields.ResumeLayout()
	Gui.frmUserFields.GsGCUserFields.MainView("gvUserFields")
	
	F.ODBC.Connection!con.Close

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.LoadUserFields.End

Program.Sub.GsGCTitles_RowCellClick.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sUserField.Declare
	V.Local.sTitle.Declare
	V.Local.sSQL.Declare
	V.Local.sCaption.Declare
	
	F.Intrinsic.Control.If(V.Global.bEdit,=,False)
		F.Intrinsic.Control.ExitSub
	F.Intrinsic.Control.EndIf

	F.ODBC.Connection!con.OpenCompanyConnection(300)
	
	F.Intrinsic.Control.SelectCase(V.Args.Column)
		F.Intrinsic.Control.Case("Value_BRW")
			F.Intrinsic.Control.If(V.DataTable.dtValues.Exists)
				F.Data.DataTable.Close("dtValues")
			F.Intrinsic.Control.EndIf
			V.Global.iRowIndex.Set(V.Args.RowIndex)
			Gui.frmUserFields.GsGCUserFields.GetCellValueByColumnName("gvUserFields","UserField",V.Global.iRowIndex,V.Local.sUserField)
			Gui.frmUserFields.GsGCUserFields.GetCellValueByColumnName("gvUserFields","Title",V.Global.iRowIndex,V.Local.sTitle)
			F.Intrinsic.String.Build("Select VID,RTrim(USER_FIELD) AS USER_FIELD, RTrim(VALUE) AS VALUE From GAB_4912_VALUES Where USER_FIELD = '{0}' Order By VALUE;",V.Local.sUserField,V.Local.sSQL)
			F.Data.DataTable.CreateFromSQL("dtValues","con",V.Local.sSQL,True)
			
			Gui.frmValues.GsGCValues.AddGridviewFromDatatable("gvValues","dtValues")
			Gui.frmValues.GsGCValues.SuspendLayout()
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","MultiSelect",True)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","Editable",False)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","ReadOnly",True)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","AllowSort",False)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","AllowFilter",True)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","OptionsDetailShowDetailTabs",False)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","OptionsViewShowGroupPanel",False)
			Gui.frmValues.GsGCValues.SetGridViewProperty("gvValues","Enableappearanceoddrow",True)
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","Visible",False)
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","Visible",False)
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","Caption","VID")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","Caption","User Field")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","Caption","Values")
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","MinWidth","45")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","MinWidth","45")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","MinWidth","250")
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","MaxLength",50)
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","HeaderFontBold",True)
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","HeaderFontBold",True)
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","HeaderFontBold",True)
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","HeaderHAlignment","Center")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","HeaderHAlignment","Center")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","HeaderHAlignment","Center")
			
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VID","CellHAlignment","Center")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","USER_FIELD","CellHAlignment","Center")
			Gui.frmValues.GsGCValues.SetColumnProperty("gvValues","VALUE","CellHAlignment","Center")
			
			Gui.frmValues.GsGCValues.ResumeLayout()
			Gui.frmValues.GsGCValues.MainView("gvValues")
			
			F.Intrinsic.Control.If(V.Local.sTitle.Trim,=,"")
				F.Intrinsic.String.Build("Select a Value: {0}",V.Local.sUserField,V.Local.sCaption)
			F.Intrinsic.Control.Else
				F.Intrinsic.String.Build("Select a Value: {0}",V.Local.sTitle,V.Local.sCaption)
			F.Intrinsic.Control.EndIf
			Gui.frmValues..Caption(V.Local.sCaption)
			Gui.frmValues..Show
	F.Intrinsic.Control.EndSelect
	
	F.ODBC.Connection!con.Close

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.GsGCTitles_RowCellClick.End

Program.Sub.cmdSave_Click.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.iCnt.Declare
	V.Local.iRow.Declare
	V.Local.sUserField.Declare
	V.Local.bClose.Declare

	F.ODBC.Connection!con.OpenCompanyConnection(300)
	
	F.Intrinsic.Control.If(V.DataTable.dtUFVals.RowCount,=,0)
		V.Local.bClose.Set(True)
		F.Data.DataTable.AddRow("dtUFVals","UFID",0,"PART",V.Global.sPart,"LOCATION",V.Global.sLoc)
	F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.For(V.Local.iCnt,1,30,1)
		F.Intrinsic.String.Build("USER{0}",V.Local.iCnt,V.Local.sUserField)
		F.Intrinsic.Math.Sub(V.Local.iCnt,1,V.Local.iRow)
		F.Data.DataTable.SetValue("dtUFVals",0,V.Local.sUserField,V.DataTable.dtUserFields(V.Local.iRow).Value!FieldValTrim)
	F.Intrinsic.Control.Next(V.Local.iCnt)
	F.Data.DataTable.SaveToDB("dtUFVals","con","GAB_4912_USER_FIELDS","UFID*!*PART*!*LOCATION")
	F.Intrinsic.UI.Msgbox("User Field Values Saved","User Field Values")
	
	F.ODBC.Connection!con.Close
	
	'If this is the first time opening the menu for that part, we want to close after saving, in order to prevent duplicate records.
	F.Intrinsic.Control.If(V.Local.bClose)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf	


	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.frmValues_UnLoad.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	Gui.frmValues..Visible(False)

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.frmValues_UnLoad.End

Program.Sub.GsGCValues_RowCellClick.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	F.Intrinsic.Control.If(V.Args.Clicks,=,2)
		F.Data.DataTable.SetValue("dtUserFields",V.Global.iRowIndex,"Value",V.Args.CellValue)
		Gui.frmValues..Visible(False)
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.GsGCValues_RowCellClick.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$triley$}$20221206143755686$}$iK6nerqp9nur1h1iSBfRKBBz1ENCgYv66JUUwFLX32g959DgQDpFTw+xV3CXDonPwmIEQYRu0a0=
Program.Sub.Comments.End